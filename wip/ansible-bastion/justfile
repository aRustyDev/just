root := `git rev-parse --show-toplevel`

pyenv_root := env("PYENV_ROOT", env("HOME") + "/.pyenv")

python_version := "3.13"
python := pyenv_root + "/shims/python3"

python3 := require("python3")
pyenv := require("pyenv")
yq := require("yq")
ansible_playbook := require("ansible-playbook")

il5_config := root + "/host_vars/bastion-1.il5.w4g.combine.map.cisco.yaml"
eng_config := root + "/host_vars/eng-bastion-1.devops.map.cisco.yaml"
dev_config := root + "/host_vars/map-combine-s-development-bastion.yaml"

il5_host := "bastion-1.il5.w4g.combine.map.cisco"
eng_host := "eng-bastion-1.devops.map.cisco"
# dev_host := "map-combine-s-development-bastion" # TODO: find the correct hostname

# Setup pyenv for (bash || zsh) w/ specified python ver
[private]
pyenv-init shell="bash":
    @grep -qxF 'export PYENV_ROOT="$HOME/.pyenv"' "~/.{{ shell }}rc" || echo 'export PYENV_ROOT="$HOME/.pyenv"' >> "~/.{{ shell }}rc"
    @grep -qxF '[[ -d $PYENV_ROOT/bin ]] && export PATH="$PYENV_ROOT/bin:$PATH"' "~/.{{ shell }}rc" || echo '[[ -d $PYENV_ROOT/bin ]] && export PATH="$PYENV_ROOT/bin:$PATH"' >> "~/.{{ shell }}rc"
    @grep -qxF 'eval "$("{{ pyenv }}" init - {{ shell }})"' "~/.{{ shell }}rc" || echo 'eval "$("{{ pyenv }}" init - {{ shell }})"' >> "~/.{{ shell }}rc"
    @"{{ pyenv }}" install "{{ python_version }}"
    @"{{ pyenv }}" local "{{ python_version }}"
    @pip install --upgrade pip
    @pip install -r requirements.txt

init: pyenv-init git-config

git-config:
    @git config user.name || git config --local user.name "$(whoami)"
    @git config user.email || echo "NEED: git config --local user.email "

add-user name user bastion role="user":
    #!/usr/bin/env bash
    val="{ name: '{{name}}', username: '{{ user }}' }"
    role="{{ if role == "admin" { "admin_" } else { "" } }}"
    cfg=$(just --evaluate "{{ bastion }}_config")
    host=$(just --evaluate "{{ bastion }}_host")
    v="$val" "{{ yq }}" -i '.authorized_{{role}}users += env(v)' "$cfg"
    "{{ansible_playbook}}" -v configure-bastion.yaml -i hosts.yaml -l "$host"
    git branch "feat/$(whoami)/add-{{user}}"
    git checkout "feat/$(whoami)/add-{{user}}"
    git add "$cfg"
    git commit -m "feat: add '{{name}}' to '{{ bastion }}' bastion as '{{ role }}' role"
    git push --set-upstream origin "feat/$(whoami)/add-{{user}}"

test-key bastion:
    #!/usr/bin/env bash
    cfg=$(just --evaluate "{{ bastion }}_config")
    host=$(just --evaluate "{{ bastion }}_host")
    ssh -i $("{{yq}}" '.root_key' "$cfg") -T $("{{yq}}" '.remote_user' "configure-bastion.yaml")@$host
